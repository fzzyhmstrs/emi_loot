plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}
evaluationDependsOn ':xplat'

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":xplat").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "emi_loot.mixins.json"
    }

    mods {
        main {
            sourceSet sourceSets.main
            sourceSet project(':xplat').sourceSets.main
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    maven {
        url "https://thedarkcolour.github.io/KotlinForForge/"
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    modCompileOnly "dev.emi:emi-forge:${rootProject.emi_version}:api"

    modRuntimeOnly "dev.emi:emi-forge:${rootProject.emi_version}"
    //modRuntimeOnly "maven.modrinth:symbols-n-stuff:${rootProject.sns_version}"

    modRuntimeOnly "maven.modrinth:kotlin-for-forge:${rootProject.kff_version}"

    modRuntimeOnly "me.fzzyhmstrs:fzzy_config:${rootProject.fzzy_config_version}+neoforge"
    //modRuntimeOnly "maven.modrinth:symbols-n-stuff:${rootProject.sns_version}"



    common(project(path: ":xplat", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":xplat", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":xplat").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}